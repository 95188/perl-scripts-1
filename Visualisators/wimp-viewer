#!/usr/bin/perl

# Copyright (C) 2010-2014 Trizen <echo dHJpemVueEBnbWFpbC5jb20K | base64 -d>.
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#-------------------------------------------------------
#  Appname: wimp-viewer
#  License: GPLv3
#  Created on: 24 August 2010
#  Latest edit on: 31 May 2014
#  https://github.com/trizen
#-------------------------------------------------------

# Requires 'youtube-viewer' and 'universal-streamer'

use 5.010;
use strict;
use warnings;

no if $] >= 5.018, warnings => 'experimental::smartmatch';

use LWP::Simple qw(get);
use XML::Fast qw(xml2hash);
use Term::ANSIColor qw(colored);
use Getopt::Std qw(getopts);

my $appname = 'wimp-viewer';
my $version = '0.32';

my $BASE_URL = 'http://www.wimp.com';

require Term::ReadLine;
my $term = Term::ReadLine->new($appname);

require WWW::Mechanize;
my $mech = WWW::Mechanize->new(
              autocheck => 1,
              env_proxy => 1,
              agent => 'Mozilla/5.0 (X11; Linux i686) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/36.0.1941.0 Safari/537.36',
);

$mech->get($BASE_URL . '/?old=1/');

sub output_usage {
    print <<"HELP";
usage: $0 [options] [url]

options:
        -f       : fullscreen mode
        -r <i>   : play i of random videos and exit

        -v       : print the version number and exit
        -h       : print this help message and exit
HELP
}

sub output_version {
    say "$appname $version";
}

my %opt;
if (@ARGV) {
    getopts('r:fvh', \%opt);
}

if ($opt{h}) {
    output_usage();
    exit 0;
}

if ($opt{v}) {
    output_version();
    exit 0;
}

if (exists $opt{r}) {

    if (defined($opt{r}) and $opt{r} > 0) {
        for my $i (1 .. $opt{r}) {
            play_random_video();
        }
    }
    else {
        die "error: option '-r' requires a positive integer!\n";
    }

    exit;
}

# Play the command-line URIs
foreach my $url (@ARGV) {
    play($url);
    exit;
}

sub play {
    my ($url) = @_;

    my $req = $mech->get($url);
    my $content = $req->decoded_content // $req->content;

    my $real_url = $mech->uri;

    if ($content =~ m{\byoutube\.com/watch\?v=([\w-]{11})"}) {
        system 'youtube-viewer', "--no-interactive", "--id=$1", ($opt{f} ? '-fs' : ());
    }
    elsif ($content =~ /"file"\h*,\h*".*?"/) {
        system 'universal-streamer', $real_url, ($opt{f} ? ('-mplayer=-fs') : ());
    }
    else {
        warn "error: can't find any streaming URL for: $real_url\n";
        return;
    }

    return 1;
}

my $hash_xml = xml2hash(get("$BASE_URL/rss"));
my @results  = @{$hash_xml->{rss}{channel}{item}};

sub play_picked_videos {
    my (@list) = @_;

    $#list >= 0 or return;
    foreach my $num (@list) {
        play($results[$num - 1]->{link});
    }

    return 1;
}

sub play_random_video {
    play("$BASE_URL/random/");
    return 1;
}

sub parse_date {
    my ($date) = @_;

    # Turns "Mon, 06 Feb 2012 00:00:00 -0600" into "Feb 06"
    if ($date =~ /^\S+ (\d+) (\S+)/) {
        return "$2 $1";
    }

    return $date // '';
}

{
    print "\n";
    my $num = 0;
    foreach my $video (@results) {
        $video->{title} =~ s/\s*\[VIDEO\]//;
        printf "%s. %s [%s]\n", colored(sprintf("%2d", ++$num), 'bold'), $video->{title}, parse_date($video->{pubDate});
    }

    {
        given ($term->readline(colored("\n=>> Insert a number ('?' for help)", 'bold') . "\n> ")) {
            when (['help', '?']) {
                print "\n", <<'STDIN_HELP';
i               : play the corresponding video
all             : play all the video results
3-8, 3..8       : same as 3 4 5 6 7 8
http://URL      : play a specific video by URL
/my?[regex]*$/  : play videos matched by a regex (/i)
q, quit, exit   : exit application
STDIN_HELP
                redo;
            }
            when (['q', 'quit', 'exit']) {
                exit 0;
            }
            when (['all']) {
                play_picked_videos(1 .. @results);
            }
            when (/\d/ and not /(?>\s|^)[^\d-]/) {
                s/(\d+)(?>-|\.\.)(\d+)/join q{ }, $1 .. $2;/eg;    # '2..5' or '2-5' to '2 3 4 5'
                play_picked_videos(grep { $_ > 0 and $_ <= @results if /^\d+$/ } split /[\s[:punct:]]+/)
                  || continue;
            }
            when (['r', 'random']) {
                play_random_video();
                redo;
            }
            when (m{^/(.*?)/\h*$}) {
                my $match = eval { qr/$1/i };

                if ($@) {
                    warn "\nError in regex: $@\n";
                    continue;
                }

                play_picked_videos(grep { $results[$_ - 1]->{'title'} =~ /$match/ } 1 .. @results) || do {
                    warn "\n(X_X) No video matched by the regex: /$match/\n";
                    redo;
                };
            }
            when (m{^https?://.}) {
                play($_);
            }
        }
    }
    redo;
}
