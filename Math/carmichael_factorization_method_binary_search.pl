#!/usr/bin/perl

# Daniel "Trizen" È˜uteu
# Date: 20 February 2019
# https://github.com/trizen

# A new integer factorization method for numbers of the form:
#
#   (x*k - 1) * (y*k - 1) * (z*k - 1)
#
# where {x,y,z} are relatively small and `k` is generated by the binary search algorithm.

# For example, many Carmichael numbers have this form and can be factorized efficiently by this method.

# See also:
#   https://en.wikipedia.org/wiki/Carmichael_number

use 5.024;
use warnings;
use experimental qw(signatures);
use Math::AnyNum qw(bsearch iroot prod gcd);

use constant {
              XMAX => 10,
              YMAX => 20,
              ZMAX => 30,
             };

my %funcs;

foreach my $x (1 .. XMAX) {
    foreach my $y ($x + 1 .. YMAX) {

        push @{$funcs{2}}, sub ($k) {
            ($x * $k - 1, $y * $k - 1);
        };

        foreach my $z ($y + 1 .. ZMAX) {
            push @{$funcs{3}}, sub ($k) {
                ($x * $k - 1, $y * $k - 1, $z * $k - 1);
            };
        }
    }
}

sub carmichael_factorization ($n) {

    no warnings 'exiting';

    foreach my $r (sort { $a <=> $b } keys %funcs) {

        foreach my $f (@{$funcs{$r}}) {

            my $factor;
            my $k = bsearch(1, iroot($n, $r),
                sub {
                    my $t = prod($f->($_));
                    my $g = gcd($t, $n);

                    if ($g > 1 and $g < $n) {
                        $factor = $g;
                        last;
                    }

                    $t <=> $n;
                }
            );

            if (defined($factor)) {
                return $factor;
            }

            if (defined($k)) {
                return $f->($k);
            }
        }
    }

    return 1;
}

say join(' ', carmichael_factorization(Math::AnyNum->new("7520940423059310542039581")));                       #=> 79443853
say join(' ', carmichael_factorization(Math::AnyNum->new("60711773123792542753")));                            #=> 2597294701
say join(' ', carmichael_factorization(Math::AnyNum->new("1000000032900000272110000405099")));                 #=> 10000000103
say join(' ', carmichael_factorization(Math::AnyNum->new("73410179782535364796052059")));                      #=> 2141993519227
say join(' ', carmichael_factorization(Math::AnyNum->new("570115866940668362539466801338334994649")));         #=> 9126423579253
say join(' ', carmichael_factorization(Math::AnyNum->new("8325544586081174440728309072452661246289")));        #=> 11153738721817
say join(' ', carmichael_factorization(Math::AnyNum->new("12946744736260953126701495197312513")));             #=> 37927921157953921
say join(' ', carmichael_factorization(Math::AnyNum->new("1169586052690021349455126348204184925097724507")));  #=> 166585508879747 1832440597677227 3831466704234203
